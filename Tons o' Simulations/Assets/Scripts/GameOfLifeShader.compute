#pragma kernel GameOfLife

/*
Rules of the Game Of Life

1. Live cells with < 2 live neigbors dies.
2. Live cells with 2 or 3 live neighbors live.
3. Live cells with > 3 live neighbors dies.
4. Dead cells with 3 live neigbors becomes alive.
*/

RWTexture2D<float4> Input;
RWTexture2D<float4> Result;

unsigned int Width, Height;

[numthreads(8, 8, 1)]
void GameOfLife(uint3 id : SV_DispatchThreadID)
{
    float4 neigborCells = float4(0, 0, 0, 1);
    
    neigborCells += Input[id.xy + float2(-1, -1)]; // Bottom Left
    neigborCells += Input[id.xy + float2( 0, -1)]; // Bottom Middle
    neigborCells += Input[id.xy + float2( 1, -1)]; // Bottom Right
    
    neigborCells += Input[id.xy + float2(-1,  0)]; // Left
    neigborCells += Input[id.xy + float2( 1,  0)]; // Right
    
    neigborCells += Input[id.xy + float2(-1,  1)]; // Top Left
    neigborCells += Input[id.xy + float2( 0,  1)]; // Top Middle
    neigborCells += Input[id.xy + float2( 1,  1)]; // Top Right
    
    // Get the alive neigbor cells
    int aliveCells = (int) neigborCells.r;
    
    // Check if current cell is alive
    if (Input[id.xy].r > 0.5)
    {
        // neigbor cells = 2 or = 3
        Result[id.xy] = (aliveCells == 2 || aliveCells == 3) ? float4(1, 1, 1, 1) : float4(0, 0, 0, 1);
    }
    else
    {
        // neigbor cells = 3
        Result[id.xy] = (aliveCells == 3) ? float4(1, 1, 1, 1) : float4(0, 0, 0, 1);
    }
}